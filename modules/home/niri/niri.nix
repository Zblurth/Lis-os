{
  host,
  config,
  pkgs,
  lib,
  ...
}:
let
  variables = import ../../../hosts/${host}/variables.nix;
  inherit (variables)
    browser
    terminal
    stylixImage
    startupApps
    monitorConfig
    ;

  barChoice = variables.barChoice or "waybar";

  # Imports
  hostKeybindsPath = ./hosts/${host}/keybinds.nix;
  hostKeybinds =
    if builtins.pathExists hostKeybindsPath then import hostKeybindsPath { inherit host; } else "";

  keybindsModule = import ./keybinds.nix {
    inherit
      host
      terminal
      browser
      barChoice
      hostKeybinds
      config
      ;
  };
  windowrulesModule = import ./windowrules.nix { inherit host; };
  layoutModule = import ./layout.nix { };
  workspacesModule = import ./workspaces.nix { };
  startupModule = import ./startup.nix {
    inherit
      host
      stylixImage
      startupApps
      barChoice
      pkgs
      ;
  };

  # Host specific overrides
  hostOutputsPath = ./hosts/${host}/outputs.nix;
  hostOutputs =
    if builtins.pathExists hostOutputsPath then
      import hostOutputsPath { inherit host; }
    else
      monitorConfig;  # <--- CHANGED FROM "" TO monitorConfig
  hostWindowRulesPath = ./hosts/${host}/windowrules.nix;
  hostWindowRules =
    if builtins.pathExists hostWindowRulesPath then
      import hostWindowRulesPath { inherit host; }
    else
      "";

in
{
  # Install Niri and related packages
  home.packages = with pkgs; [
    niri
    udiskie
    xwayland-satellite
    swww
    grim
    slurp
    wl-clipboard
    swappy
  ];

  # Generate Niri config.kdl
  xdg.configFile."niri/config.kdl".text = ''
    // Niri Configuration for ${host}
    // Generated by NixOS configuration

    ${hostOutputs}
    ${workspacesModule}
    ${layoutModule}
    ${keybindsModule}
    ${windowrulesModule}
    ${hostWindowRules}
    ${startupModule}

    environment {
          XDG_CURRENT_DESKTOP "niri"

          // --- Graphic & Toolkit Flags ---
          // These ensure apps launched by Niri use Wayland
          GTK_USE_PORTAL "1"
          MOZ_ENABLE_WAYLAND "1"
          NIXOS_OZONE_WL "1"
          ELECTRON_OZONE_PLATFORM_HINT "wayland"

          QT_QPA_PLATFORM "wayland"
          QT_QPA_PLATFORMTHEME "gtk3"
          QT_QPA_PLATFORMTHEME_QT6 "gtk3"

          TERMINAL "${terminal}"
          XCURSOR_THEME "Bibata-Modern-Ice"
          XCURSOR_SIZE "24"

          // NVIDIA Gaming Optimizations
          __GL_GSYNC_ALLOWED "1"
          __GL_VRR_ALLOWED "1"
          PROTON_ENABLE_NVAPI "1"
          PROTON_HIDE_NVIDIA_GPU "0"
          PROTON_ENABLE_NGX_UPDATER "1"
    }

    hotkey-overlay {
        skip-at-startup
    }

    prefer-no-csd
    screenshot-path "~/Pictures/Screenshots/Screenshot from %Y-%m-%d %H-%M-%S.png"
  '';

  # Systemd services for Niri session
  systemd.user.targets.niri-session = {
    Unit = {
      Description = "Niri compositor session";
      Documentation = "man:systemd.special(7)";
      BindsTo = "graphical-session.target";
      Wants = "graphical-session-pre.target";
      After = "graphical-session-pre.target";
    };
  };

  # Waybar service
  systemd.user.services.waybar-niri = lib.mkIf (barChoice == "waybar") {
    Unit = {
      Description = "Waybar status bar (Niri session)";
      PartOf = "graphical-session.target";
      After = "graphical-session.target";
      ConditionEnvironment = "XDG_CURRENT_DESKTOP=niri";
    };
    Service = {
      ExecStart = "${pkgs.waybar}/bin/waybar";
      Restart = "on-failure";
      RestartSec = "1s";
    };
    Install.WantedBy = [ "graphical-session.target" ];
  };

  # DMS service
  systemd.user.services.dms-niri = lib.mkIf (barChoice == "dms") {
    Unit = {
      Description = "DMS Desktop Management Shell (Niri session)";
      PartOf = "graphical-session.target";
      After = "graphical-session.target";
      ConditionEnvironment = "XDG_CURRENT_DESKTOP=niri";
    };
    Service = {
      ExecStart = "${config.home.homeDirectory}/.local/bin/dms run";
      Restart = "on-failure";
      RestartSec = "1s";
    };
    Install.WantedBy = [ "graphical-session.target" ];
  };

  # XWayland satellite service
  systemd.user.services.xwayland-satellite = {
    Unit = {
      Description = "Xwayland outside Wayland";
      BindsTo = "graphical-session.target";
      After = "graphical-session.target";
    };
    Service = {
      Type = "notify";
      NotifyAccess = "all";
      ExecStart = "${pkgs.xwayland-satellite}/bin/xwayland-satellite";
      StandardOutput = "journal";
      Restart = "on-failure";
    };
    Install.WantedBy = [ "graphical-session.target" ];
  };

  # Environment preloading service - runs BEFORE portals start
  systemd.user.services.niri-env-setup = {
    Unit = {
      Description = "Set up environment for Niri session";
      Before = [
        "xdg-desktop-portal.service"
        "xdg-desktop-portal-gtk.service"
        "xdg-desktop-portal-gnome.service"
      ];
      Requires = [ "graphical-session.target" ];
      After = [ "graphical-session.target" ];
    };
    Service = {
      Type = "oneshot";
      RemainAfterExit = true;
      ExecStart = pkgs.writeShellScript "setup-niri-env" ''
        # Set all critical Niri/Wayland environment variables
        export XDG_CURRENT_DESKTOP=niri
        export XDG_SESSION_DESKTOP=niri
        export XDG_SESSION_TYPE=wayland

        # Update DBus activation environment
        ${pkgs.dbus}/bin/dbus-update-activation-environment --systemd \
          XDG_CURRENT_DESKTOP \
          XDG_SESSION_DESKTOP \
          XDG_SESSION_TYPE \
          WAYLAND_DISPLAY \
          DISPLAY

        # Import into systemd user instance
        ${pkgs.systemd}/bin/systemctl --user import-environment \
          XDG_CURRENT_DESKTOP \
          XDG_SESSION_DESKTOP \
          XDG_SESSION_TYPE \
          WAYLAND_DISPLAY \
          DISPLAY
      '';
    };
    Install.WantedBy = [ "graphical-session.target" ];
  };

  # Place wallpapers in home directory
  home.file = {
    "Pictures/Wallpapers" = {
      source = ../../../wallpapers;
      recursive = true;
    };
  };
}
